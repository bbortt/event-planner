plugins {
    id "com.github.ben-manes.versions" version "0.28.0"
    id "org.sonarqube"
}

group = "io.github.bbortt.event.planner"

description = "Event Planning Tool."

apply from: "gradle/sonar.gradle"

allprojects {
    apply plugin: "com.github.ben-manes.versions"

    version = "${version}"

    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            gradlePluginPortal()
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    pluginManager.withPlugin("java", {
        sourceCompatibility = 11
        targetCompatibility = 11
        assert System.properties["java.specification.version"] == "1.8" || "11" || "12" || "13" || "14" || "15"

        dependencies {
            annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"

            implementation platform("tech.jhipster:jhipster-dependencies:${jhipsterDependenciesVersion}")

            implementation "io.dropwizard.metrics:metrics-core"
            implementation "io.micrometer:micrometer-registry-prometheus"
            implementation "org.springframework.boot:spring-boot-starter-actuator"
        }

        compileJava.dependsOn processResources

        test {
            useJUnitPlatform()
            exclude "**/*IT*", "**/*IntTest*", "**/*IntegrationTest*"

            testLogging {
                events "FAILED", "SKIPPED"
            }
            // uncomment if the tests reports are not generated
            // see https://github.com/jhipster/generator-jhipster/pull/2771 and https://github.com/jhipster/generator-jhipster/pull/4484
            // ignoreFailures true
            reports.html.enabled = false
        }

        task integrationTest(type: Test) {
            useJUnitPlatform()
            description = "Execute integration tests."
            group = "verification"
            include "**/*IT*", "**/*IntTest*", "**/*IntegrationTest*"

            testLogging {
                events "FAILED", "SKIPPED"
            }

            // uncomment if the tests reports are not generated
            // see https://github.com/jhipster/generator-jhipster/pull/2771 and https://github.com/jhipster/generator-jhipster/pull/4484
            // ignoreFailures true
            reports.html.enabled = false
        }
        integrationTest.dependsOn test

        check.dependsOn integrationTest

        // Regarding: https://github.com/reactor/BlockHound/issues/33
        tasks.withType(Test).all {
            if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_13)
                || JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_14)
                || JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_15)) {
                jvmArgs += [
                    "-XX:+AllowRedefinitionToAddDeleteMethods"
                ]
            }
        }

        task testReport(type: TestReport) {
            destinationDir = file("$buildDir/reports/tests")
            reportOn test
        }

        task integrationTestReport(type: TestReport) {
            destinationDir = file("$buildDir/reports/tests")
            reportOn integrationTest
        }

        configurations {
            providedRuntime
            implementation.exclude module: "spring-boot-starter-tomcat"
        }
    })
}


if (project.hasProperty("prod")) {
    apply from: "gradle/profile_prod.gradle"
} else {
    apply from: "gradle/profile_dev.gradle"
}

defaultTasks "check"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task cleanResources(type: Delete) {
    delete "build/resources"
}

wrapper {
    gradleVersion = "6.8.3"
}
