plugins {
    id "io.spring.dependency-management" version "1.0.14.RELEASE"
    id "jacoco"
    id "java"
    id "org.flywaydb.flyway"
    id "org.openapi.generator"
    id "org.sonarqube"
    id "org.springframework.boot" version "2.7.3"
}

sourceCompatibility = "${sourceCompatibility}"
assert System.properties["java.specification.version"] == "11" || "12" || "13" || "14" || "15" || "16" || "17"

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:2021.0.1")
    implementation "io.swagger.core.v3:swagger-annotations:2.2.2"
    implementation "jakarta.validation:jakarta.validation-api:2.0.2"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "org.openapitools:jackson-databind-nullable:0.2.2"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-undertow"
    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.postgresql:postgresql"
    testImplementation "org.bitbucket.b_c:jose4j:0.7.12"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.cloud:spring-cloud-contract-wiremock"
    testImplementation "org.springframework.security:spring-security-test"
}

openApiValidate {
    inputSpec = "${project.projectDir}/oas/spec.yml".toString()
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${project.rootDir}/specs/projects-service-v1.yml".toString()
    outputDir = "${project.buildDir}/generated-sources".toString()
    apiPackage = "io.github.bbortt.event.planner.apps.projects.v1.rest"
    invokerPackage = "io.github.bbortt.event.planner.apps.projects.v1"
    modelNameSuffix = "Dto"
    modelPackage = "io.github.bbortt.event.planner.apps.projects.v1.dto"
    configOptions = [
            dateLibrary    : "java8",
            delegatePattern: "true",
            interfaceOnly  : "true",
            useOptional    : "true",
            useTags        : "true"
    ]
}
compileJava.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDirs += "${project.buildDir}/generated-sources/src/main/java"

flyway {
    locations = ["filesystem:src/main/resources/db/migration"]
}

task flywayMigrateDev(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    url = "jdbc:postgresql://localhost:5432/event_planner"
    user = "event_planner"
    password = "event_planner_password"
}

task flywayMigrateTest(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    url = "jdbc:postgresql://localhost:5432/event_planner_test"
    user = "event_planner"
    password = "event_planner_password"
}

task flywayCleanTest(type: org.flywaydb.gradle.task.FlywayCleanTask) {
    url = "jdbc:postgresql://localhost:5432/event_planner_test"
    user = "event_planner"
    password = "event_planner_password"
}

apply from: "gradle/testing.gradle"
apply from: "gradle/sonar.gradle"
